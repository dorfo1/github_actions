apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.8.7"

    configurations.all {
        resolutionStrategy {
            eachDependency { details ->
                if ('org.jacoco' == details.requested.group) {
                    details.useVersion "0.8.7"
                }
            }
        }
    }
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

project.afterEvaluate {

    if (!project.hasProperty("android")) return

    // Grab all build types and product flavors
    def buildTypes = android.buildTypes.collect { type ->
        type.name
    }
    def productFlavors = android.productFlavors.collect { flavor ->
        flavor.name
    }
    // When no product flavors defined, use empty
    if (!productFlavors) productFlavors.add('')
    productFlavors.each { productFlavorName ->
        buildTypes.each { buildTypeName ->
            def sourceName
            if (!productFlavorName) {
                sourceName = "${buildTypeName}"
            } else {
                sourceName = "${productFlavorName}${buildTypeName.capitalize()}"
            }
            def localTestCoverageTask = "test${sourceName.capitalize()}UnitTest"
            def uiTestCoverageTask = "create${sourceName.capitalize()}CoverageReport"

            def tasksArray
            def jacocoDir
            if (project.android.buildTypes.debug.testCoverageEnabled) {
                tasksArray = ["$localTestCoverageTask" , "$uiTestCoverageTask"] //Uncomment for enable coverage for Instrumented tests
                jacocoDir = "/outputs/unit_test_code_coverage/${sourceName}UnitTest/*.exec"
            } else {
                tasksArray = ["$localTestCoverageTask"]
                jacocoDir = "/jacoco/${localTestCoverageTask}.exec"
            }

            // Create coverage task 'testFlavorTypeCoverage'
            tasks.create(name: "test${sourceName.capitalize()}Coverage", type: JacocoReport, dependsOn: tasksArray) {
                group = "Reporting"
                description = "Generate Jacoco coverage reports on the ${sourceName.capitalize()} build."

                def fileFilter = [
                        '**/R.class',
                        '**/R$*.class',
                        '**/BuildConfig.*',
                        '**/Manifest*.*',
                        '**/*Test*.*',
                        'android/**/*.*',
                        '**/*Creator.class',
                        '**/*\$Lambda$*.*',
                        '**/*\$inlined$*.*',
                        '**/*Initialization$init*', //código gerado pelo Koin
                        '**/*Initialization*', //código gerado pelo Koin
                ]
                def debugTree = fileTree(dir: "${buildDir}/intermediates/javac/$sourceName/classes", excludes: fileFilter)
                def kotlinDebugTree = fileTree(dir: "${buildDir}/tmp/kotlin-classes/$sourceName", excludes: fileFilter)

                classDirectories.setFrom(files([debugTree], [kotlinDebugTree]))

                def javaSrc = [
                        "src/main/java",
                        "src/$productFlavorName/java"
                ]

                def kotlinSrc = [
                        "src/main/kotlin",
                        "src/$productFlavorName/kotlin"
                ]

                additionalSourceDirs.setFrom(files([javaSrc, kotlinSrc]))
                sourceDirectories.setFrom(files([javaSrc, kotlinSrc]))

                "/outputs/unit_test_code_coverage/${sourceName}UnitTest/*.exec"

                executionData.setFrom(fileTree(dir: project.buildDir, includes: [
                        jacocoDir, // Local test
                        "/outputs/code_coverage/${sourceName}AndroidTest/connected/*.ec" // Instrumented test
                ]))

                reports {
                    xml.enabled = true
                    html.enabled = true
                }

                doLast() {
                    print "file://${reports.html.destination}/index.html\n"
                }
            }
        }
    }
}
